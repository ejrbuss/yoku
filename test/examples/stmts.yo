--- mode Repl ---

--- test "Variable Declarations" ---
z --> ! ResolutionError
var x = true
x --> true
const y = 12
y --> 12
const _ = 7
const a: Int = "try again" --> ! TypeError
const a: Bool = true

--- test "Tuple destructuring" ---
const (x, y, z) = (1, 2, 3)
(x, y, z) --> (1, 2, 3)
const (x, y) = (true, 1, 4) --> ! TypeError
const (_, w) = (true, false)
w --> false
const (a, b) as ab = ("a", "b")
a --> "a"
b --> "b"
ab --> ("a", "b")

--- test "Assert Statments" ---
assert 1 == 1
assert true == false --> ! RuntimeError
const x = 4
const y = 7
const z = "t"
assert x == y --> ! RuntimeError
assert x < y
assert x == z --> ! TypeError

--- test "Assert Type Casting" ---
const x: Any = 42
const y: Int = x --> ! TypeError
const assert z: Int = x
z --> 42
const assert u: Str = x --> ! RuntimeError
const assert v: Any = z
v --> 42

--- test "Assert Destructuring" ---
const 1 = 1 --> ! ResolutionError
const assert 1 = 1
const assert (1, 2) = (1, 2)
const assert (true, x) = (true, "t")
x --> "t"
const assert (1, 1) = (1, 2) --> ! RuntimeError

--- test "Assignment" ---
var a = "before"
a --> "before"
a = "after"
a --> "after"
a = 4 --> ! TypeError
const b = "constant"
b = "oops!" --> ! ResolutionError

--- test "Block Scope" ---
var collector = ""
do {
	const a = "outer"
	do {
		const a = "inner"
		collector = cat(collector, a)
	}
	collector = cat(collector, a)
}
collector --> "innerouter"

--- test "Cannot Shadow Builtins" ---
const print = 10 --> ! ResolutionError
do {
	const cat = 7
} --> ! ResolutionError

--- test "Can Shadow Non-Builtins" ---
const x = 1
x --> 1
const x = 2
x --> 2
const x = x * 2
x --> 4
const y = y --> ! ResolutionError

--- test "If Statements" ---
if true { "then" } else { throw () } --> "then"
if false { throw () } else { "else" } --> "else"
const x = if 3 > 4 { "a" } else { "b"; "c" }
x --> "c"

const Condition = true
if Condition { 1 } else { 2 }

--- test "Loop Statments" ---
var count = 0
var secret = 0
loop {
	if count > 10 {
		break
	}
	count = count + 1
	if (count > 5) {
		continue
	}
	secret = secret + 1
}
count --> 11
secret --> 5

loop outer {
	loop inner {
		break outer
	}
}

--- test "While Statements" ---
var count = 0
while count < 10 {
	count = count + 1
}
count --> 10

var secret = 0
while count < 20 {
	count = count + 1
	if (count < 15) {
		continue
	}
	secret = secret + 1
}
secret --> 6

--- test "Combining Loops" ---
var counter = 1
loop outer {
	while true {
		counter = counter + 1
		if counter < 5 {
			continue
		}
		loop inner {
			break outer
		}
	}
}
counter --> 5

--- test "Invalid Control Flow" ---
break --> ! ResolutionError
continue --> ! ResolutionError
loop {
	break outer
} --> ! ResolutionError
while true {
	continue outer
} --> ! ResolutionError

--- test "Match Expressions" ---
match {
	if true => {}
	else => { throw () }
}

match () {
	_ => {}
	_ => { throw () }
}

const x = 5
match x {
	1 => { "one" }
	2 => { "two" }
	else => { "many" }
} --> "many"

match x {
	_ if x % 2 == 0 => { "even" }
	_ if x % 2 == 1 => { "odd" }
	else => { "dunno" }
} --> "odd"

const y = 12
match {
	if x > y => { 1 }
	if y > x => { 2 }
	else => { 3 }
} --> 2

--- test "If Declarations" ---
proc f(x: Any) -> Str {
	if const y: Bool = x {
		cat("Bool ", y)
	} else if var y: Int = x {
		cat("Int ", y)
	} else {
		cat("Dunno ", x)
	}
}
f(true) --> "Bool true"
f(1) --> "Int 1"
f("test") --> "Dunno test"

--- test "Combined Assignment Operators" ---
var x = 1
x += 1
x --> 2
x -= 7
x --> -5
x *= 4 
x --> -20
x /= 2
x --> -10
x %= 3
x --> -1

var y = true
y &= false
y --> false
y |= true
y --> true